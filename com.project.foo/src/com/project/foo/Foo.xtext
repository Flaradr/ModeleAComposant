grammar com.project.foo.Foo with org.eclipse.xtext.common.Terminals

generate foo "http://www.project.com/foo/Foo"

//Concerne les modeles
DomainModel:
	model=Model;

/*
 * Structure d'un modele, correspond
 * à un paquet pouvant contenir plusieurs
 * composants et assemblages
 */
Model:
	'package' name=QualifiedName '{'
	(imports+=Import)*
	(components+=Component)*
	(assembly+=Assembly)*
	'}';

	
QualifiedName:
    ID ('.' ID)*;

Import:
    'import' importedNamespace=QualifiedNameWithWildcard;
    
QualifiedNameWithWildcard:
    QualifiedName '.*'?;


//Structure d'un assemblage
Assembly:
	{Assembly}'Assembly' name=ID '{'
	'components'
	attributes+=ComponentInstance
	(attributes+=ComponentInstance)+
	'bindings'
	(bindings+=Binding)*
//	(bindingsRequiered += BindingRequiered '-' bindingsProvided += BindingProvided)*
	'}';

//Structure d'un composant
ComponentInstance:
	name=ID ':' component=[Component|QualifiedName];

//Lien entre d'un service requis d'un composant X et d'un
//service fourni d'un composant Y
Binding:
	bindingRequiered=BindingRequiered '-' bindingProvided=BindingProvided ;

//Instance du composant ayant besoin d'un service
BindingRequiered:
	name=[ComponentInstance]"."service=[RSignature|QualifiedName];

//Instance du composant fournissant un service
BindingProvided:
	name=[ComponentInstance]"."service=[PSignature|QualifiedName];

//Structure d'un composant
Component:
	'Component' name=ID '{'
	listOfPServices = ListOfProvidedServices
	listOfRServices = ListOfRequieredServices
	(mProvServices += MProvidedService)+
	(mReqServices += MRequieredService)*
	'}';

//Liste des services fourni par un composant X
ListOfProvidedServices:
	'provided' '=' '{'
		(providedServices+=ProvidedService
		(',' providedServices+=ProvidedService)*)
	'}';

ProvidedService:
	name=[PSignature];
	
//Liste des services requis par un composant X
ListOfRequieredServices:
	{ListOfRequieredServices}'requiered' '=' '{'
		(requieredServices+=RequieredService?
		(',' requieredServices+=RequieredService)*)
	'}';

RequieredService:
	name=[RSignature];
	
//Signature d'un service fourni
MProvidedService:
	'service' 'provided' signature=PSignature'{''}';

//Signature d'un service fourni
PSignature:
	type=ID name=ID'('parameters+=Parameter?(','parameters+=Parameter)*')';

MRequieredService:
	'service' 'requiered' signature=RSignature  ';';

//Signature d'un service requis
RSignature:
	type=ID name=ID'('parameters+=Parameter?(','parameters+=Parameter)*')';

//Parametre dans une signature 
Parameter:
	name=ID ':' type=ID;
