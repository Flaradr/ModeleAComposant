grammar com.project.foo.Foo with org.eclipse.xtext.common.Terminals

generate foo "http://www.project.com/foo/Foo"

//Concerne les modeles
DomainModel:
	(models+=Model)*;

/*
 * 
 * Structure d'un modele, correspond
 * à un paquet pouvant contenir plusieurs
 * composants et assemblages
 */
Model:
	'package' name=QualifiedName '{'
	(imports+=Import)*
	(component+=Component)*
	(assembly+=Assembly)*
	'}';

QualifiedName:
    ID ('.' ID)*;

Import:
    'import' 'static' 'extension' importedNamespace=FqnWithWildCard;

FqnWithWildCard:
    QualifiedName '.*'?;

//Structure d'un assemblage
Assembly:
	{Assembly}'Assembly' name=ID '{'
	'components'
	attributes+=ComponentAttribute
	(attributes+=ComponentAttribute)+
	'bindings'
	(bindings+=Binding)*
	'}';

//Structure d'un composant
ComponentAttribute:
	name=ID ':' type=[Component|QualifiedName];

//Lien entre d'un service requis d'un composant X et d'un
//service fourni d'un composant Y
Binding:
	mG=BindingRequiered '-' mD=BindingProvided ;

BindingRequiered:
	name=[ComponentAttribute]'.'type=[RequieredService|QualifiedName];

BindingProvided:
	name=[ComponentAttribute]'.'type=[ProvidedService|QualifiedName];

//Structure d'un composant
Component:
	'Component' name=ID '{'
	provided = Provided
	requiered = Requiered
	(mProvServices += MProvidedService)+
	(mReqServices += MRequieredService)*
	'}';

//Liste des services fourni par un composant X
Provided:
	'provided' '=' '{'
		(providedServices+=ProvidedService
		(',' providedServices+=ProvidedService)*)
	'}';

ProvidedService:
	name=ID;

//Liste des services requis par un composant X
Requiered:
	{Requiered}'requiered' '=' '{'
		(requieredServices+=RequieredService?
		(',' requieredServices+=RequieredService)*)
	'}';

RequieredService:
	name=ID;

MProvidedService:
	'service' 'provided' signature=PSignature'{'
	'}';

//Signature d'un service fourni
PSignature:
	type=ID name=[ProvidedService]'('attributes+=Attribute?(','attributes+=Attribute)*')';

MRequieredService:
	'service' 'requiered' signature=RSignature  ';';

//Signature d'un service requis
RSignature:
	type=ID name=[RequieredService]'('attributes+=Attribute?(','attributes+=Attribute)*')';

Attribute:
	name=ID ':' type=ID;
