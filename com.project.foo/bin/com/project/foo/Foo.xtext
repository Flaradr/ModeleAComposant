grammar com.project.foo.Foo with org.eclipse.xtext.common.Terminals

generate foo "http://www.project.com/foo/Foo"

//Concerne les modeles
File:
	(models+=Model)*;

/*
 * 
 * Structure d'un modele, correspond
 * à un paquet pouvant contenir plusieurs
 * composants et assemblages
 */
Model:
	'package' packageName=QualifiedName '{'
	(imports+=Import)*
	(component+=Component)*
	(assembly+=Assembly)*
	'}';

QualifiedName:
    ID ('.' ID)*;

Import:
    'import' importedNamespace=FqnWithWildCard;

FqnWithWildCard:
    QualifiedName '.*'?;

//Structure d'un assemblage
Assembly:
	{Assembly}'Assembly' name=ID '{'
	'components'
	attributes+=ComponentInstance
	(attributes+=ComponentInstance)+
	'bindings'
	(bindings+=Binding)*
	'}';

//Structure d'un composant
ComponentInstance:
	name=QualifiedName ':' composant=[Component|QualifiedName];

//Lien entre d'un service requis d'un composant X et d'un
//service fourni d'un composant Y
Binding:
	mG=BindingRequiered '-' mD=BindingProvided ;

//Instance du composant ayant besoin d'un service
BindingRequiered:
	id=[ComponentInstance|QualifiedName]'->'service=[RequieredService|QualifiedName];

//Instance du composant fournissant un service
BindingProvided:
	id=[ComponentInstance|QualifiedName]'->'service=[ProvidedService|QualifiedName];

//Structure d'un composant
Component:
	'Component' name=ID '{'
	listOfPServices = ListOfProvidedServices
	listOfRServices = ListOfRequieredServices
	(mProvServices += MProvidedService)+
	(mReqServices += MRequieredService)*
	'}';

//Liste des services fourni par un composant X
ListOfProvidedServices:
	'provided' '=' '{'
		(providedServices+=ProvidedService
		(',' providedServices+=ProvidedService)*)
	'}';

ProvidedService:
	name=ID;

//Liste des services requis par un composant X
ListOfRequieredServices:
	{ListOfRequieredServices}'requiered' '=' '{'
		(requieredServices+=RequieredService?
		(',' requieredServices+=RequieredService)*)
	'}';

RequieredService:
	name=ID;

//Signature d'un service fourni
MProvidedService:
	'service' 'provided' signature=PSignature'{''}';

//Signature d'un service fourni
PSignature:
	type=ID name=[ProvidedService]'('attributes+=Attribute?(','attributes+=Attribute)*')';

MRequieredService:
	'service' 'requiered' signature=RSignature  ';';

//Signature d'un service requis
RSignature:
	type=ID name=[RequieredService]'('attributes+=Attribute?(','attributes+=Attribute)*')';

Attribute:
	id=ID ':' type=ID;
